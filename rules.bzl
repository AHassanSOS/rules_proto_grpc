ProtoPluginInfo = provider(fields = {
    "name": "proto plugin name",
    "outputs": "outputs to be generated",
    "tool": "plugin tool",
    "options": "proto options",
})

ProtoCompileInfo = provider(fields = {
    "plugins": "ProtoPluginInfo object",
    "grpc_plugins": "ProtoPluginInfo object",
    "descriptor": "descriptor set file",
    "outputs": "generated outputs",
    "protos": "generated protos (copies)",
    "args": "proto arguments",
    "tools": "proto tools",
})


def _proto_plugin_impl(ctx):
    return [ProtoPluginInfo(
        name = ctx.label.name,
        options = ctx.attr.options,
        outputs = ctx.attr.outputs,
        tool = ctx.executable.tool,
    )]

proto_plugin = rule(
    implementation = _proto_plugin_impl,
    attrs = {
        "options": attr.string_list(
            doc = "An list of options to pass to the compiler.",
        ),
        "outputs": attr.string_list(
            doc = "The file extension generated by output files.  Example: '{basename}_pb2.py'",
        ),
        "tool": attr.label(
            doc = "The plugin binary.  If absent, assume the plugin is a built-in to protoc itself",
            cfg = "host",
            executable = True,
        ),
    }
)


# Hack - providers indexing is by int, but I have not idea how to get the actual
# provider object here.
ProtoInfoProvider = 0

def _capitalize(s):
  """Capitalize a string - only first letter
  Args:
    s (string): The input string to be capitalized.
  Returns:
    (string): The capitalized string.
  """
  return s[0:1].upper() + s[1:]


def _pascal_case(s):
    """Convert pascal_case -> PascalCase
    Args:
        s (string): The input string to be capitalized.
    Returns:
        (string): The capitalized string.
    """
    return "".join([_capitalize(part) for part in s.split("_")])

def _get_output_sibling_file(pattern, proto, descriptor):
    if pattern.startswith("@package/"):
        return descriptor
    return proto


def _get_output_filename(src, pattern):
    # Slice off this prefix if it exists, we don't use it here.
    if pattern.startswith("@package/"):
        pattern = pattern[len("@package/"):]
    basename = src.basename
    if basename.endswith(".proto"):
        basename = basename[:-6]
    elif basename.endswith(".protodevel"):
        basename = basename[:-11]

    filename = basename
   
    if pattern.find("{basename}") != -1:
        filename = pattern.replace("{basename}", basename)
    elif pattern.find("{basename|pascal}") != -1:
        filename = pattern.replace("{basename|pascal}", _pascal_case(basename))
    else:
        filename = basename + pattern

    return filename

def _get_proto_filename(src):
    #print("src.short_path: %s" % src.short_path)
    parts = src.short_path.split("/")
    if len(parts) > 1 and parts[0] == "..":
        return "/".join(parts[2:])
    return src.short_path

def copy_proto(ctx, descriptor, src):
    proto = ctx.actions.declare_file(_get_proto_filename(src), sibling = descriptor)
    ctx.actions.run_shell(
        mnemonic = "CopyProto",
        inputs = [src],
        outputs = [proto],
        command = "cp %s %s" % (src.path, proto.path),
    )
    return proto

def get_plugin_out_arg(outdir, plugin):
    arg = outdir
    if plugin.options:
        arg = "%s:%s" % (",".join(plugin.options), arg) 
    return "--%s_out=%s" % (plugin.name, arg)  


proto_compile_attrs = {
    "deps": attr.label_list(
        mandatory = True,
        providers = ["proto"],
    ),
    "plugins": attr.label_list(
        providers = [ProtoPluginInfo],
        mandatory = True,
    ),
    "grpc_plugins": attr.label_list(
        providers = [ProtoPluginInfo],
        mandatory = False,
    ),
    "outputs": attr.output_list(
    ),
    "has_services": attr.bool(
        doc = "If the proto files(s) have a service rpc, generate grpc outputs",
    ),
    "protoc": attr.label(
        default = "@com_google_protobuf//:protoc",
        cfg = "host",
        executable = True,
    ),
}

proto_compile_outputs = {
    "descriptor": "%{name}/descriptor.bin",
}

def proto_compile_impl(ctx):
    protoc = ctx.executable.protoc
    has_services = ctx.attr.has_services
    descriptor = ctx.outputs.descriptor
    outdir = descriptor.dirname
    deps = [dep.proto for dep in ctx.attr.deps]
    plugins = [plugin[ProtoPluginInfo] for plugin in ctx.attr.plugins]
    grpc_plugins = [plugin[ProtoPluginInfo] for plugin in ctx.attr.grpc_plugins]
    tools = [protoc]
    protos = []
    outputs = []
    args = []
    directs = {}

    for dep in deps:
        for src in dep.direct_sources:
            if directs.get(src.path):
                continue
            directs[src.path] = src
            proto = copy_proto(ctx, descriptor, src)
            protos.append(proto)

            for plugin in plugins:
                for output in plugin.outputs:
                    sibling = _get_output_sibling_file(output, proto, descriptor)
                    outputs.append(ctx.actions.declare_file(_get_output_filename(src, output), sibling = sibling))

            if has_services:
                for plugin in grpc_plugins:
                    for output in plugin.outputs:
                        sibling = _get_output_sibling_file(output, proto, descriptor)
                        outputs.append(ctx.actions.declare_file(_get_output_filename(src, output), sibling = sibling))

        for src in dep.transitive_sources:
            if directs.get(src.path):
                continue
            #print("transitive source: %r" % src)
            proto = copy_proto(ctx, descriptor, src)
            protos.append(proto)
        for e in dep.transitive_proto_path:
            print("proto_path: %r" % e)
        # for e in dep.transitive_descriptor_sets:
        #     print("descriptor_set: %r" % e)

    args += ["--descriptor_set_out=%s" % descriptor.path]
    args += ["--proto_path=%s" % outdir]        
    for plugin in plugins:
        args += [get_plugin_out_arg(outdir, plugin)]        
    if has_services:
        for plugin in grpc_plugins:
            args += [get_plugin_out_arg(outdir, plugin)]        
            if plugin.tool:    
                args += ["--plugin=protoc-gen-%s=%s" % (plugin.name, plugin.tool.path)]        
                tools += [plugin.tool]

    args += [proto.path for proto in protos]

    ctx.actions.run_shell(
        command = "find . && " +  " ".join([protoc.path] + args) + " && find .",
        #command = " ".join([protoc.path] + args),
        inputs = tools + protos,
        outputs = outputs + [descriptor] + ctx.outputs.outputs,
    )

    return [ProtoCompileInfo(
        plugins = plugins,
        grpc_plugins = grpc_plugins,
        protos = protos,
        outputs = outputs,
        tools = tools,
        args = args,
        descriptor = descriptor,
    ), DefaultInfo(files = depset(outputs + ctx.outputs.outputs))]

proto_compile = rule(
    implementation = proto_compile_impl,
    attrs = proto_compile_attrs,
    outputs = proto_compile_outputs,
    output_to_genfiles = True,
)